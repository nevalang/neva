// https://www.99-bottles-of-beer.net

component Main(start) (stop) {
    nodes { Match<int>, next PrintNext2Lines }

    :start -> (99 -> next:n -> match:data)
    -1 -> match:case[0] -> :stop
    match:else -> next:n
}

component PrintNext2Lines(n int) (n int) {
    nodes {
        decr   Decr<int>
        first  PrintFirstLine
        second PrintSecondLine
    }
    :n -> first:n -> decr:data
    decr:res -> second:n -> :n
}

// === First Line ===

const firstLine1 string = '$0 bottles of beer on the wall, $0 bottles of beer.\n'
const firstLine2 string = '1 bottle of beer on the wall, 1 bottle of beer.'
const firstLine3 string = 'No more bottles of beer on the wall, no more bottles of beer.'

component PrintFirstLine(n int) (n int) {
    nodes { Match<int>, Println, Printf, Lock<int> }

    :n -> [match:data, lock:data]

    0 -> match:case[0] -> ($firstLine3 -> println:data)
    1 -> match:case[1] -> ($firstLine2 -> println:data)
    match:else -> [
        printf:args[0],
        ($firstLine1 -> printf:tpl)
    ]

    [println:sig, printf:args[0]] -> lock:sig
    lock:data -> :n
}

// === Second Line ===

const secondLine1 string = 'Take one down and pass it around, $0 bottles of beer on the wall.\n\n'
const secondLine2 string = 'Take one down and pass it around, 1 bottle of beer on the wall.\n'
const secondLine3 string = 'Take one down and pass it around, no more bottles of beer on the wall.\n'
const secondLine4 string = 'Go to the store and buy some more, 99 bottles of beer on the wall.'

component PrintSecondLine(n int) (n int) {
    nodes { Match<int>, Lock<int>, Printf, Println }

    :n -> [match:data, lock:data]

    -1 -> match:case[0] -> ($secondLine4 -> println:data)
    0  -> match:case[1] -> ($secondLine3 -> println:data)
    1  -> match:case[2] -> ($secondLine2 -> println:data)
    match:else -> [
        printf:args[0],
        ($secondLine1 -> printf:tpl)
    ]

    [println:sig, printf:args[0]] -> lock:sig
    lock:data -> :n
}